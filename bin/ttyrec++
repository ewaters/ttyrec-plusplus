#!/usr/bin/env perl

use warnings;
use strict;
use Time::HiRes qw(gettimeofday tv_interval);
use JSON::XS;
use Encode qw(encode decode);
use Data::Dumper;
use POSIX;
use POE qw(Wheel::ReadWrite Wheel::Run Filter::Stream);
use Getopt::Long;
use utf8;

## Configuration

my %opt = (
	out_file    => "ttyrec.$$.log",
	window_size => undef,
	command     => $ENV{SHELL},
);

GetOptions(
	'out=s'     => \$opt{out_file},
	'command=s' => \$opt{command},
);

if (@ARGV) {
	$opt{command} = \@ARGV;
}
if (! $opt{window_size}) {
	my ($rows, $cols) = `/bin/stty size` =~ m{^(\d+) \s+ (\d+)}x;
	$opt{window_size} = [ $rows * 1, $cols * 1, 1024, 768 ];
}

## Setup

my $json_xs = JSON::XS->new->ascii;
open my $log, '>', $opt{out_file} or die "Failed to open $opt{out_file} for writing: $!";
my $start_time = [gettimeofday];

print "Creating log $opt{out_file}\n";

### Start a session to encapsulate the previous features.

POE::Session->create(
	inline_states => {
		_start             => \&handle_start,
		_stop              => \&handle_stop,
		got_terminal_stdin => \&handle_terminal_stdin,
		got_child_stdout   => \&handle_child_stdout,
		got_sigchld        => \&handle_sigchld,
	},
);

$poe_kernel->run();

exit 0;

sub record_input {
	do_log('stdin', @_);
}

sub record_output {
	do_log('stdout', @_);
}

sub do_log {
	my ($from, $text) = @_;

	my $seconds_since_start = tv_interval($start_time, [gettimeofday]);
	my $ms_since_start = sprintf('%d', $seconds_since_start * 1000) * 1;

	printf $log "recorded_%s(%s);\n", (
		($from eq 'stdin' ? 'input' : 'output'),
		$json_xs->encode([ $ms_since_start, decode('utf8', $text) ]),
	);
}

### Handle the _start event.  This sets things in motion.

sub handle_start {
	my ($kernel, $heap) = @_[KERNEL, HEAP];

	# Set a signal handler.
	$kernel->sig(CHLD => "got_sigchld");

	# Save the original terminal settings so they can be restored later.
	$heap->{stdin_tio} = POSIX::Termios->new();
	$heap->{stdin_tio}->getattr(0);
	$heap->{stdout_tio} = POSIX::Termios->new();
	$heap->{stdout_tio}->getattr(1);
	$heap->{stderr_tio} = POSIX::Termios->new();
	$heap->{stderr_tio}->getattr(2);

	# Put the terminal into raw input mode.  Otherwise discrete
	# keystrokes will not be read immediately.

	my $tio = POSIX::Termios->new();
	$tio->getattr(0);

	my $lflag = $tio->getlflag;
	$lflag &= ~(ECHO | ECHOE | ECHOK | ECHONL | ICANON | IEXTEN | ISIG);
	$tio->setlflag($lflag);

	my $iflag = $tio->getiflag;
	$iflag &= ~(BRKINT | INPCK | ISTRIP | IXON);
	$tio->setiflag($iflag);

	my $cflag = $tio->getcflag;
	$cflag &= ~(CSIZE | PARENB);
	$tio->setcflag($cflag);
	$tio->setattr(0, TCSANOW);

	# Start the terminal reader/writer.

	$heap->{stdio} = POE::Wheel::ReadWrite->new(
		InputHandle  => \*STDIN,
		OutputHandle => \*STDOUT,
		InputEvent   => "got_terminal_stdin",
		Filter       => POE::Filter::Stream->new(),
	);

	# Start the asynchronous child process.

	$heap->{program} = POE::Wheel::Run->new(
		Program     => $opt{command},
		Conduit     => "pty",
		Winsize     => $opt{window_size},
		StdoutEvent => "got_child_stdout",
		StdioFilter => POE::Filter::Stream->new(),
	);
}

### Handle the _stop event.  This restores the original terminal
### settings when we're done.  That's very important.

sub handle_stop {
	my $heap = $_[HEAP];
	$heap->{stdin_tio}->setattr(0, TCSANOW);
	$heap->{stdout_tio}->setattr(1, TCSANOW);
	$heap->{stderr_tio}->setattr(2, TCSANOW);
}

### Handle terminal STDIN.  Send it to the background program's STDIN.

sub handle_terminal_stdin {
	my ($heap, $input) = @_[HEAP, ARG0];
	record_input($input);
	$heap->{program}->put($input);
}

### Handle STDOUT from the child program.  Send it to the terminal's
### STDOUT.

sub handle_child_stdout {
	my ($heap, $input) = @_[HEAP, ARG0];
	record_output($input);
	$heap->{stdio}->put($input);
}

### Handle SIGCHLD.  Shut down if the exiting child process was the
### one we've been managing.

sub handle_sigchld {
	my ($heap, $child_pid) = @_[HEAP, ARG1];
	if ($child_pid == $heap->{program}->PID) {
		delete $heap->{program};
		delete $heap->{stdio};
	}
	return 0;
}

